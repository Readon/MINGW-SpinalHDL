_realname=python-sby
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver="0.33"
pkgrel=1
pkgdesc="Front-end for Yosys-based formal verification flows"
arch=("any")
mingw_arch=('mingw64' 'ucrt64')
url="https://github.com/YosysHQ/sby"
groups=("${MINGW_PACKAGE_PREFIX}-eda")
license=("custom:ISC")
#options=("!strip")
depends=(
  "${MINGW_PACKAGE_PREFIX}-yosys"
  "${MINGW_PACKAGE_PREFIX}-python"
)
optdepends=(
	"${MINGW_PACKAGE_PREFIX}-boolector"
	"${MINGW_PACKAGE_PREFIX}-yices"
	"${MINGW_PACKAGE_PREFIX}-z3"
)
makedepends=("git")
source=(
	"${_realname}::git+https://github.com/YosysHQ/sby.git#tag=yosys-${pkgver}"
	"0001-fix-msys2-runtime-problem.patch"
)
sha256sums=(
	"SKIP"
	"SKIP"
)

prepare() {
  cd "${srcdir}"/${_realname}

  patch -Np1 -i "${srcdir}"/0001-fix-msys2-runtime-problem.patch
}

package() {
	cd "${srcdir}/${_realname}"
	make PREFIX="${MINGW_PREFIX}" DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/${MINGW_PREFIX}/share/yosys/licenses/LICENSE"
  
	files=$(find "${pkgdir}${MINGW_PREFIX}"/bin -type f -name "*-script.py")
	if [ -n "$files" ]; then
		for _f in "$files"; do
			sed -e '1 { s/^#!.*$// }' -i ${_f}
		done
	fi
}
